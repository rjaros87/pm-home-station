plugins {
    id 'edu.sc.seis.macAppBundle'
    id 'edu.sc.seis.launch4j'
    id 'com.github.johnrengelman.shadow'
    id 'java'
    id 'application'
}

group 'pmstation'
version '1.3.0-alpha'

def local_appName = 'PM Home Station'
def local_mainClassName = 'pmstation.Start'
def local_projectUrl = 'https://github.com/rjaros87/pm-home-station'

sourceCompatibility = 1.8
mainClassName = local_mainClassName

repositories {
    mavenCentral()
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs

    // xchart lib versions >3.5.0 started to have transitive dependency to pdfbox and fontbox (those libs caused shadowjar to grow from ~8MB to 12MB)
    compile.exclude module: 'pdfbox'
    compile.exclude module: 'fontbox'
}

shadowJar {
    baseName = project.name
    classifier = null
    version = version
    
    manifest {
        attributes 'Implementation-Title': "${local_appName} - the standalone application (uber) JAR"
        attributes 'Implementation-Version': version
        attributes 'Implementation-Vendor': local_projectUrl
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Built-JDK': System.getProperty('java.version')
        attributes 'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
    
    exclude 'META-INF/*.txt'
}

launch4j {
    copyConfigurable = project.tasks.shadowJar.outputs.files
    mainClassName = local_mainClassName
    icon = "${projectDir}/icons/app-icon.ico"
    outfile = "${project.name}-${version}.exe"
    supportUrl = local_projectUrl
    companyName = local_projectUrl
    version = version
    textVersion = version
    productName = local_appName
    copyright = 'Copyright (C), licence: GPL-3.0'
}

macAppBundle {
    appName = local_appName
    mainClassName = local_mainClassName
    runtimeConfigurationName = 'shadow'
    jarTask = 'shadowJar'
    icon = 'icons/dmg-myIcon.icns'
    bundleJRE = false
    javaProperties.put('apple.laf.useScreenMenuBar', 'true')
    backgroundImage = 'icons/dmg-bg.png'
    appIconX = 50
    appIconY = 120
    appFolderX = 330
    appFolderY = 120
}

dependencies {
    extraLibs group: 'org.knowm.xchart', name: 'xchart', version: '3.6.2'
    extraLibs group: 'com.fazecast', name: 'jSerialComm', version: '2.6.2'
    extraLibs group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    extraLibs group: 'com.miglayout', name: 'miglayout-swing', version: '5.2'
    extraLibs group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
    extraLibs group: 'org.apache.commons', name: 'commons-configuration2', version: '2.7'
    extraLibs group: 'commons-io', name: 'commons-io', version: '2.6'
    extraLibs group: 'commons-cli', name: 'commons-cli', version: '1.4'
    extraLibs group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    extraLibs group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    extraLibs group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    // it is required by configuration2.builder
    extraLibs group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

    extraLibs project(':core')
    configurations.compile.extendsFrom(configurations.extraLibs)
    testCompile group: 'junit', name: 'junit', version: '4.13'
}
