plugins {
//    id 'edu.sc.seis.macAppBundle'
    id 'edu.sc.seis.launch4j'
    id 'com.github.johnrengelman.shadow'
    id 'java'
    id 'application'
}

group 'pmstation'
version '1.3.1-alpha'

def local_appName = 'PM Home Station'
def local_mainClassName = 'pmstation.Start'
def local_projectUrl = 'https://github.com/rjaros87/pm-home-station'

// keeping 17 it allow this desktop app to run on 32-bit RPi Zero (armv6l)
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
mainClassName = local_mainClassName

repositories {
    mavenCentral()
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs

    // xchart lib versions >3.5.0 started to have transitive dependency to pdfbox and fontbox (those libs caused shadowjar to grow from ~8MB to 12MB)
    compile.exclude module: 'pdfbox'
    compile.exclude module: 'fontbox'
}

// for :run task
run {
    jvmArgs = [
        "--add-exports=java.desktop/com.apple.eawt=ALL-UNNAMED"
    ]
}

compileJava {
    options.encoding = "UTF-8"
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set(null)
    archiveVersion.set(project.version)

    manifest {
        attributes 'Implementation-Title': "${local_appName} - the standalone application (uber) JAR"
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor': local_projectUrl
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Built-JDK': System.getProperty('java.version')
        attributes 'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }

    exclude 'META-INF/*.txt'
}

launch4j {
    // https://github.com/TheBoegl/gradle-launch4j?tab=readme-ov-file#configurable-input-configuration
    copyConfigurable = []
    jarTask = project.tasks.shadowJar

    mainClassName = local_mainClassName
    icon = "${projectDir}/icons/app-icon.ico"
    outfile = "${project.name}-${project.version}.exe"
    supportUrl = local_projectUrl
    companyName = local_projectUrl
    version = project.version
    textVersion = project.version
    productName = local_appName
    copyright = 'Copyright (C), licence: GPL-3.0'
}

//macAppBundle {
//    appName = local_appName
//    mainClassName = local_mainClassName
//    runtimeConfigurationName = 'shadow'
//    jarTask = 'shadowJar'
//    icon = 'icons/dmg-myIcon.icns'
//    bundleJRE = false
//    javaProperties.put('apple.laf.useScreenMenuBar', 'true')
//    backgroundImage = 'icons/dmg-bg.png'
//    appIconX = 50
//    appIconY = 120
//    appFolderX = 330
//    appFolderY = 120
//}

dependencies {
    extraLibs group: 'org.knowm.xchart', name: 'xchart', version: '3.6.2'
    extraLibs group: 'com.fazecast', name: 'jSerialComm', version: '2.11.0'
    extraLibs group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    extraLibs group: 'com.miglayout', name: 'miglayout-swing', version: '5.2'
    extraLibs group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
    extraLibs group: 'org.apache.commons', name: 'commons-configuration2', version: '2.7'
    extraLibs group: 'commons-io', name: 'commons-io', version: '2.6'
    extraLibs group: 'commons-cli', name: 'commons-cli', version: '1.4'
    extraLibs group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    extraLibs group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    extraLibs group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    // it is required by configuration2.builder
    extraLibs group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    extraLibs group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.5'
    extraLibs group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

    extraLibs project(':core')
    configurations.implementation.extendsFrom(configurations.extraLibs)
    testImplementation group: 'junit', name: 'junit', version: '4.13'
}
